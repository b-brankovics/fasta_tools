#!/usr/bin/perl -w
use strict;

#===DESCRIPTION=================================================================
# A tool to format FASTA file to replace names from duplicate removal
#  by fasta_unique. Using the produced fasta file and the names table.

my $description = 
    "Description:\n\t" .
    "A tool to format FASTA file to replace names from duplicate removal\n\t".
    " by fasta_unique. Using the produced fasta file and the names table.\n";
my $usage = 
    "Usage:\n\t$0 -i (FASTA file)|-  -tab (names table)\n";
my $options = 
    "Options:\n" .
    "\t-h | --help\n\t\tPrint the help message; ignore other arguments.\n" .
    "\n";
#===MAIN========================================================================

# Print help if needed
&print_help(@ARGV);

# Hash to store the sequences: key (id and defintion) and value (sequence) 
my %fas_data;
# Array to store the order of the sequences
my @ids;

# If there were arguments specified then read the sequence from them
# OR read from STDIN
if (@ARGV != 4) {
    die "ERROR: incorrect number of arguments:\n" . $usage;
}

my $in;
my $list;

my $c=0;
for (@ARGV) {
    if (/^-?-t(ab)?$/) {$list = $ARGV[$c+1]}
    if (/^-?-i(nput)?$/) {$in = $ARGV[$c+1]}
    $c++;
}

&read_fasta(\%fas_data, \@ids, $in);

# Get the sequence group data from the table file
my %multi;
my $tab;
if ($list eq "-") {
    print STDERR "Reading tab info from STDIN\n";
    $tab = *STDIN;
} else {
    open($tab, '<', $list);
}
for (<$tab>) {
    s/\R//g;
    next if /^\s*$/;
    my @a = split /\t/;
    $multi{$a[0]} = $a[1];
}
close $tab;

# Go through and print the sequences
for my $id (@ids) {
    # If ID is in the table file
    #  Then print a copy for each entry in the group
    if ($multi{$id}) {
	for (split /,/, $multi{$id}) {
	    print &to_fasta($_, $fas_data{$id});
	}
    } else {
	    print &to_fasta($id, $fas_data{$id});
    }
}




#===SUBROUTINES=================================================================

sub print_help {
    # Print out the usage to STDERR
    # Takes in the @ARGV as input
    my @args = @_;
    for (@args) {
	if (/^-?-h(elp)?$/) {
	    die "$usage\n$description\n$options";
	}
    }
}

sub read_fasta {
    # Convert FASTA string into a hash with IDs for keys and sequences
    #  as values and stores the original order in an array
    # This subroutine requires three arguments:
	#	1) filehandle for the FASTA file
	#	2) a hash reference to store the sequences in
	#	3) an array reference to store the IDs in the same
	#          order as the original file 
    # If an ID line is present multiple times then a warning is printed
    #  to STDERR
    my ($hash, $list, $file) = @_;
    # Use STDIN if file is '-'
    $file = undef if $file && $file eq '-';
    my $in;
    if ($file) {
	if ( -e $file) {
	    open $in, '<', $file || die $!;
	} else {
	    die "ERROR: there is no '$file' file\n";
	}
    } else {
	$in = *STDIN;
    }
    # Store the sequence id
    my $seq_id;
    for (<$in>) {
	# Remove line endings
	s/\R//g;
	# Skip empty lines
	next if /^\s*$/;
	# Check wheter it is an id line
	if (/>(.*)/) {
	    # Save the id and the definition and store it in the array
	    $seq_id = $1;
	    print {*STDERR} "WARNING: <$seq_id> is present in multiple copies\n" if $hash->{$seq_id};
	    push @$list, $seq_id;
	} else {
	    # If there was no id lines before this then throw an error
	    unless (defined $seq_id) {
		print "Format error in FASTA file! Check the file!\n";
		last;
	    }
	    # Remove white space
	    s/\s+//g;
	    # Add to the sequence
	    $hash->{$seq_id} .= $_;
	}
    }
    close $in;
}

sub to_fasta {
    # Return a fasta formated string
    my ($seq_name, $seq, $len) = @_;
    # default to 60 characters of sequence per line
    $len = 60 unless $len;
    # Print ID line
    my $formatted_seq = ">$seq_name\n";
    # Add sequence lines with $len length
    while (my $chunk = substr($seq, 0, $len, "")) {
	$formatted_seq .= "$chunk\n";
    }
    return $formatted_seq;
}
