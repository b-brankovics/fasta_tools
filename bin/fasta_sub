#!/usr/bin/perl -w
use strict;

#===DESCRIPTION=================================================================
# A tool to extract a part of the sequences from FASTA files

my $description = 
    "Description:\n\tA tool to extract a part of the sequences from FASTA files\n";
my $usage = 
    "Usage:\n\t$0 [-h | --help] [FASTA file | -] (<int> <int> | <int>..<int>)\n";
my $options = 
    "Options:\n" .
    "\t-h | --help\n\t\tPrint the help message; ignore other arguments.\n" .
    "\n";
#===MAIN========================================================================

# Hash to store the sequences: key (id and defintion) and value (sequence) 
my %fas_data;
# Array to store the order of the sequences
my @ids;

# Print help if needed
&print_help(@ARGV);

# Differentiate between input file(s) and sequence positions 
my @requests;
my @input;
for (@ARGV) {
    if (/^(\d+)$/) {
	push @requests, $1;
    } elsif (/^(\d+)\.\.(\d+)$/) {
	push @requests, $1;
	push @requests, $2;
    } else {
	push @input, $_;
    }
}
@ARGV = @input;

# There should be exactly two positions specified
die "ERROR: Exactly two positions should be specified!\n" .
    "\t" . (scalar @requests) . " positions were specified\n$usage" unless scalar @requests == 2;

# If there were arguments specified then read the sequence from them
# OR read from STDIN
if (@ARGV) {
    for (@ARGV) {
	&read_fasta(\%fas_data, \@ids, $_);
    }
} else {
    &read_fasta(\%fas_data, \@ids);
}

# Get the region and print the sequence to STDOUT
#  Check the orientation of the region
#    Start and stop are needed for the extraction step
my ($a, $b) = @requests;
my ($start, $stop, $rev);
if ($a > $b) {
    ($start, $stop) = ($b, $a);
    $rev++;
} else {
    ($start, $stop) = ($a, $b)
}
for (@ids) {
    # Print error if the sequence is too short
    print STDERR "WARNING: <$_> is too short for the start position\n" if $start > length $fas_data{$_};
    print STDERR "WARNING: <$_> is too short for the stop position\n" if $stop > length $fas_data{$_};
    # Get the requested chunk
    my $chunk = substr $fas_data{$_}, $start -1, $stop - $start + 1;
    # Print result according to orientation
    if ($rev) {
	print &to_fasta("$_ $a..$b", &reverse_seq($chunk));
    } else {
	print &to_fasta("$_ $a..$b", $chunk);
    }
}


#===SUBROUTINES=================================================================

sub print_help {
    # Print out the usage to STDERR
    # Takes in the @ARGV as input
    my @args = @_;
    for (@args) {
	if (/-?-h(elp)?/) {
	    die "$usage\n$description\n$options";
	}
    }
}

sub read_fasta {
    # Convert FASTA string into a hash with IDs for keys and sequences
    #  as values and stores the original order in an array
    # This subroutine requires three arguments:
	#	1) filehandle for the FASTA file
	#	2) a hash reference to store the sequences in
	#	3) an array reference to store the IDs in the same
	#          order as the original file 
    # If an ID line is present multiple times then a warning is printed
    #  to STDERR
    my ($hash, $list, $file) = @_;
    # Use STDIN if file is '-'
    $file = undef if $file && $file eq '-';
    my $in;
    if ($file && -e $file) {
	open $in, '<', $file || die $!;
    } else {
	$in = *STDIN;
    }
    # Store the sequence id
    my $seq_id;
    for (<$in>) {
	# Skip empty lines
	next if /^\s*$/;
	# Check wheter it is an id line
	if (/>(.*)/) {
	    # Save the id and the definition and store it in the array
	    $seq_id = $1;
	    print {*STDERR} "WARNING: <$seq_id> is present in multiple copies\n" if $hash->{$seq_id};
	    push @$list, $seq_id;
	} else {
	    # If there was no id lines before this then throw an error
	    unless (defined $seq_id) {
		print "Format error in FASTA file! Check the file!\n";
		last;
	    }
	    # Remove line endings and white space
	    s/\R//g;
	    s/\s+//g;
	    # Add to the sequence
	    $hash->{$seq_id} .= $_;
	}
    }
    close $in;
}

sub to_fasta {
    # Return a fasta formated string
    my ($seq_name, $seq, $len) = @_;
    # default to 60 characters of sequence per line
    $len = 60 unless $len;
    # Print ID line
    my $formatted_seq = ">$seq_name\n";
    # Add sequence lines with $len length
    while (my $chunk = substr($seq, 0, $len, "")) {
	$formatted_seq .= "$chunk\n";
    }
    return $formatted_seq;
}

sub print_fasta {
    # Print all the sequences to STDOUT in FASTA format
    my ($hash, $list) = @_;
    for (@$list) {
	print &to_fasta($_, $hash->{$_});
    }
}

sub reverse_seq {
    # Reverse complements the sequences
    my ($seq) = @_;
    # Reverse the sequnce
    my $complement = reverse $seq;
    # Complement the sequence
    $complement =~ tr/ACGTacgtWwMmRrSsKkYyBbVvDdHh/TGCAtgcaWwKkYySsMmRrVvBbHhDd/;
    return $complement;
}
